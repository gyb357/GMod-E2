@name ACF Tank Turret FCS V4 Alpha 1.3
@inputs  [Pod Cam]:wirelink [Base Turret Gun]:entity [ChangeCam Zoom Hold Trig]
@outputs [OpsCpu]:array [Th RkTh CTh Hit Run]

@persist [Elevation Depression RotateSpeed YawClamp CamOffset CamDistance ZoomCount MagnificMax FCSMod HoldMod Parent Kp Dt Increase Tolerance] [RotateSound]:string
@persist [Gv]:vector2 [G Pi Caliber DragDiv Range Magnific RotateYaw]

@persist [Chair Driver]:entity [CamMod ZIndex GunHold]
@persist [Ranger]:ranger
@persist [Rk4]:array [BPos BVel EndPos]:vector2 [Th RkTh CTh Hit Run]

interval(50)

if (first() | dupefinished()) {
  Elevation   = 90
  Depression  = 20
  RotateSpeed = 5
  YawClamp    = 360

  CamOffset   = 400
  CamDistance = 150

  ZoomCount   = 3
  MagnificMax = 80

  FCSMod  = 1
  # 1 = direct ballistics
  # 2 = artillery ballistics
  HoldMod = 1
  # 1 = Rotate at the same angle as the base
  # 2 = Rotate to last aim position
  Parent = 1

  Kp = 1
  Dt = 0.001
  Increase  = 0.001
  Tolerance = 1

  RotateSound = "acf_extra/tankfx/turret5.wav"

    Gv = vec2(0, -gravity())
    G  = gravity()
    Pi = pi()
    Caliber = Gun:acfCaliber()
    DragDiv = acfDragDiv()

    Range = 50000
    Magnific  = MagnificMax/ZoomCount
    RotateYaw = YawClamp/2

  function array rk4(Comp:array, DragCoef, Dt) {
    P1 = Comp[1, vector2], V1 = Comp[2, vector2]
    K  = V1:length()*DragCoef

    A1 = Gv - K*V1
    V2 = V1 + A1*Dt/2, A2 = Gv - K*V2
    V3 = V1 + A2*Dt/2, A3 = Gv - K*V3
    V4 = V1 + A3*Dt,   A4 = Gv - K*V4
    Xf = P1 + (Dt/6)*(V1 + 2*(V2 + V3) + V4)
    Vf = V1 + (Dt/6)*(A1 + 2*(A2 + A3) + A4)
    return array(Xf, Vf)
  }

  function void setHoloTurret() {
    holoCreate(1, Gun:pos()),     holoCreate(2, Turret:pos())
    #holoParent(1, holoEntity(2)), holoParent(2, Base)

    Gun:deparent(), Turret:deparent()
    if (Parent) {Gun:parentTo(holoEntity(1)), Turret:parentTo(holoEntity(2))}
  }

  function void setRangerFilter() {
    rangerFilter(Gun)
    rangerFilter(Base:getConstraints())
    rangerPersist(1)
  }

  setHoloTurret()
  setRangerFilter()
}

  Active = Pod["Active", number]
if (changed(Active) & Active) {
  Chair  = Pod["Entity", entity]
  Driver = Chair:driver()

  Cam["parent", entity] = Base

  ZIndex  = 0
  FOV     = 90
  GunHold = 0
}

if (Active) {
  if (~ChangeCam & ChangeCam)   {CamMod = !CamMod}
  if (changed(CamMod) & CamMod) {
    CamDist                 = CamMod ? 0 : CamDistance
    Cam["Distance", number] = CamDist
  }
  CamPosition             = Base:pos() + vec(0, 0, CamOffset)
  Cam["Position", vector] = CamPosition

  if (~Zoom & Zoom)       {ZIndex += 1}
  if (ZIndex > ZoomCount) {ZIndex = 1}
  if (changed(ZIndex) & ZIndex) {
    FOV                = 90 - ZIndex*Magnific
    Cam["FOV", number] = FOV
  }

  if (~Hold & Hold) {GunHold = !GunHold}
  if (!GunHold) {
    EyeAng = Chair:toLocal(Driver:eyeAngles())
    Ranger = rangerOffset(Range, CamPosition, EyeAng:forward())
  }

  TPos = Ranger:pos()
  GPos = Gun:attachmentPos("muzzle")

  if (FCSMod) {
    Sign = Trig ? 1 : -1
    Mv = Gun:acfMuzzleVel()*39.37
    Dx = sqrt((TPos:x() - GPos:x())^2 + (TPos:y() - GPos:y())^2)
    Dy = TPos:z() - GPos:z()
    Th = atan((Mv^2 + Sign*sqrt(Mv^4 - G*(G*Dx^2 + 2*Dy*Mv^2)))/(G*Dx))
    Tf = Dx/Mv*cos(Th)

    if (FCSMod == 2) {
      Dk = Dy/Dx
      Mass = Gun:acfProjectileMass()
      Drag = (Pi*Caliber^2)/(4000000*Mass*DragDiv)

      if (changed(Th) & Th) {Run = 1}
      while (perf() & Run) {
        Rk4  = rk4(Rk4, Drag, Dt)
        BPos = Rk4[1, vector2]
      }
      if (BPos:y() < Dk*BPos:x()) {
        EndPos = BPos
        RkTh   = (Dx - EndPos:x())*Increase
        CTh    = Th + RkTh
        Rk4    = array(vec2(0, 0), vec2(Mv*cos(CTh), Mv*sin(CTh)))
      }
      RkDxm = (Dx - EndPos:x())*0.01905
      Hit   = (RkDxm < Tolerance) ? 1 : 0
      
      if (isnan(CTh) | isinf(CTh) | RkDxm > 100 | Hit) {Run = 0}

      if (!Run & !Hit) {Rk4 = array(vec2(0, 0), vec2(Mv*cos(Th), Mv*sin(Th)))}
    }
    else {CTh = Th}
  }
}

  GunAng = GunHold ? Base:angles() : (TPos - GPos):toAngle() + ang(CTh, 0, 0)
  #Force  = Gun:toWorld(clamp(Gun:toLocal(GunAng), ang(-RotateSpeed), ang(RotateSpeed)))
  #Angle  = clamp(Base:toLocal(Force), ang(-Elevation, -RotateYaw, -1), ang(Depression, RotateYaw, 1))
  #Local  = Gun:toLocal(Base:toWorld(Angle))

  holoAng(1, GunAng*ang(1, 1, 0)) # Gun
  #holoAng(2, ang()) # Turret

  OpsCpu = array(ops(), cpuUsage()*10^6)
