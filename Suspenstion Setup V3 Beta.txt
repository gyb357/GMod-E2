@name Suspenstion Setup V3 Beta
@inputs  [Wheels]:array [Chassis]:entity
@persist [ApplyForTank CreateCons PrintDebug DeleteE2 ConstBreak Rope Elastic Ballsocket Nocollide]:number
@persist [Forward Right Up Pitch Yaw Roll Spring Damping Relative Width]:number [Material]:string [PlusVecter]:vector
@persist [ConsArray]:array [ConsCount WheelCount]:number [NilVec]:vector
@persist [ConsBreakCore ConsBreakIndex RopeIndex ElasticIndex BallsocketIndex NoCollideIndex]:number
@persist [Core Index]:array [HoloPos HoloAng]:array

if (first() | dupefinished()) {
  # ----- Action Setting ----- #
  ApplyForTank = 1
  CreateCons   = 1
  PrintDebug   = 1
  DeleteE2     = 0

  # ----- Constraints Setting ----- #
  ConstBreak = 1
  Rope       = 1
  Elastic    = 1
  Ballsocket = 1
  Nocollide  = 1

  # ----- Suspenstion Setting ----- #
    # Position
    Forward = 30
    Right   = 10
    Up      = 10

    # Angle
    Pitch = 0
    Yaw   = 0
    Roll  = 0

    # Elastic
    Spring   = 50000
    Damping  = 5000
    Relative = 1000
    PlusVecter = vec(0, 0, 0)

    # Draw
    Width    = 0
    Material = "cable/cable2"

  # ----- Don't modify below part ----- #
    ConsArray  = array("Rope", "Elastic", "ADVballsocket", "Nocollide")
    ConsCount  = ConsArray:count()
    WheelCount = Wheels:count()
    NilVec     = vec(0.01, 0.01, 0.01)

  function void array:consBreak() {
    #ifdef entity:isConstrainedTo(string) & entity:constraintBreak(string)
    ConsBreakCore = 1
    for (I = 1, WheelCount) {
      local WhlEnt = Wheels[I, entity]

      for (J = 1, ConsCount) {
        local ConsStr = ConsArray[J, string]

        if (WhlEnt:isConstrainedTo(ConsStr)) {WhlEnt:constraintBreak(ConsStr), ConsBreakIndex++}
      }
    }
    #else
    ConsBreakCore = 0
    #endif
  }

  function vector localPos(Sign:number, Ent:entity) {
    return Chassis:toLocal(Ent:massCenter()) + vec(Forward, Right*Sign, Up):rotate(Pitch, Yaw, Roll)
  }

  function void array:setRope() {
    #ifdef rope(number, entity, vector, entity, vector, number, number, string, number)
    for (I = 1, WheelCount) {
      local WhlEnt  = Wheels[I, entity]

      for (J = 1, 2) {
        local Sign    = J == 1 ? 1 : -1
        local ToLocal = localPos(Sign, WhlEnt)

        RopeIndex++

        rope(RopeIndex, WhlEnt, WhlEnt:massCenterL(), Chassis, ToLocal, 0, Width, Material, 1)
      }
    }
    #endif
  }

  function void array:setElastic(Vector:vector) {
    #ifdef hydraulic(number, entity, vector, entity, vector, number, number, number, string, number, number)
    for (I = 1, WheelCount) {
      local WhlEnt  = Wheels[I, entity]
      local ToLocal = localPos(1, WhlEnt) + Vector

      ElasticIndex++

      hydraulic(ElasticIndex, WhlEnt, WhlEnt:massCenterL(), Chassis, ToLocal, Spring, Damping, Relative, Material, Width, 0)
    }
    #endif
  }

  function void array:setBallsocket() {
    #ifdef ballsocket(entity, vector, entity, vector, vector, vector, number)
    for (I = 1, WheelCount) {
      local WhlEnt = Wheels[I, entity]

      if (ApplyForTank) {
        if (Chassis) {
          ballsocket(WhlEnt, NilVec, Chassis, -vec(180, 0.01, 0.01), vec(180, 0.01, 0.01), NilVec, 1)
          BallsocketIndex++
        }
        ballsocket(Wheels[1+I, entity], NilVec, Sprocket, -NilVec, NilVec, NilVec, 1)
        BallsocketIndex++
      }
    }
    #endif
  }

  function void array:setNocollide() {
    #ifdef noCollide(entity, entity)
    for (I = 1, WheelCount) {
      local WhlEnt = Wheels[I, entity]

      if (ApplyForTank) {
        local AftWhl = Wheels[I+1, entity]

        noCollide(WhlEnt, AftWhl), NoCollideIndex++
      }
      else {
        noCollide(WhlEnt, Chassis), NoCollideIndex++
      }
    }
    #endif
  }

  function void printDebug() {
    # Color
    local Red   = vec(250,  0,   0 )
    local Green = vec(150, 250, 150)
    local White = vec(250, 250, 250)

    local C_ConsBreak  = ConsBreakCore ? Green : Red
    local C_Rope       = Rope          ? Green : Red
    local C_Elastic    = Elastic       ? Green : Red
    local C_Ballsocket = Ballsocket    ? Green : Red
    local C_Nocollide  = Nocollide     ? Green : Red

    # Title
    printColor(White, "[Suspenstion Setup V3]")

    # Constraints
    printColor(
      White,        "Constraints: ",
      C_ConsBreak,  "Deleted "      + ConsBreakIndex  + " Constraints", White, " / ",
      C_Rope,       "Rope("         + RopeIndex       + ")",            White, " / ", 
      C_Elastic,    "Elastic("      + ElasticIndex    + ")",            White, " / ",
      C_Ballsocket, "Ballsocket("   + BallsocketIndex + ")",            White, " / ",
      C_Nocolide,   "Nocolide("     + NoCollideIndex  + ")"
    )
  }

  if (ConstBreak) {Wheels:consBreak()}
  if (CreateCons) {
    if (Rope)       {Wheels:setRope()}
    if (Elastic)    {Wheels:setElastic(PlusVecter)}
    if (Ballsocket) {Wheels:setBallsocket()}
    if (Nocolide)   {Wheels:setNocollide()}
  }
  if (PrintDebug) {printDebug()}
}

if (dupefinished()) {
  if (DeleteE2) {
    #ifdef entity:propDelete()
    entity():propDelete()
    hint("Deleted Suspenstion Setup V3 E2 ", 3)
    #endif
  }
}

# Made by RuBid Suspenstion Setup V3 Beta V2020.10.03

